group "com.resty.service"
version "0.1"

apply plugin: "java"
apply plugin: "checkstyle"
apply plugin: "findbugs"
apply plugin: "org.springframework.boot"
apply plugin: "jacoco"

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    ext {
        springBootVersion = "1.5.3.RELEASE"
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.spring.io/release" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.spring.io/release" }
}

jar {
    baseName = 'services'
    version =  '0.0.0'
}

springBoot { // this makes the jar build as an executable
    executable = true
}

checkstyle {
    toolVersion = "6.0"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
    executionData = files("build/jacoco/test.exec", "build/jacoco/intTest.exec")
}

// output details about unchecked and deprecated method calls
gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

// integration test configuration - in separate source directory
sourceSets {

    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
    }

}

// integration test compile and runtime configurations
configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

configurations {
    all*.exclude group: "org.hibernate", module: "hibernate-entitymanager"
    all*.exclude group: "org.apache.tomcat", module: "tomcat-jdbc"
}

dependencies {
    compile (
            "org.springframework.boot:spring-boot-starter-actuator",
            "org.springframework.boot:spring-boot-starter-jdbc",
            // Securing rest resources can be done by hooking in spring-boot-starter-security
            "org.springframework.boot:spring-boot-starter-security",
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-mail",
            "org.springframework:spring-jms",
            "com.google.guava:guava:18.0",
            "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.8",
            "ma.glasnost.orika:orika-core:1.4.6",
            "org.projectlombok:lombok:1.16.6",
            "org.jadira.usertype:usertype.core:4.0.0.GA",
            "org.jdbi:jdbi:2.78",
            "org.antlr:stringtemplate:3.2.1",
            "commons-lang:commons-lang:2.6",
            "com.amazonaws:aws-java-sdk:1.11.127",
            "org.postgresql:postgresql:9.3-1102-jdbc41",
            "org.liquibase:liquibase-core:3.4.1",
            "com.jcraft:jsch:0.1.54"
    )

    runtime(
            "com.zaxxer:HikariCP:2.4.3"
    )

    testCompile(
            "org.springframework.boot:spring-boot-starter-test",
            "org.exparity:hamcrest-date:2.0.4"
    )

    integrationTestCompile(
            "org.springframework.boot:spring-boot-starter-test",
            "org.exparity:hamcrest-date:2.0.4",
            "com.h2database:h2"
    )

    checkstyle("com.puppycrawl.tools:checkstyle:6.14.1")
}

// integration test task that runs the integration tests (with correct classpath)
task intTest(type: Test) {
    if ( project.hasProperty('jvmArgs') ) {
        jvmArgs = (project.jvmArgs.split("\\s+") as List)
    }
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false } // run the intTests every time
    testLogging.showStandardStreams = true
}

// ensure check task fails the build if there are failing integration tests
check.dependsOn intTest
// ensure integration tests run after unit tests
intTest.mustRunAfter test

findbugs {
    excludeFilter = file("$projectDir/config/findbugs/excludeFilter.xml")
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// create HTML reports of unit and integration tests in different directories
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}